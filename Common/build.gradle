plugins {
    id 'multiloader-common'
    id 'org.spongepowered.gradle.vanilla' version '0.2.1-SNAPSHOT'
}

def useLocalYungsApi = findProject(':yungsapi') != null

minecraft {
    version(mc_version)
    def aw = file("src/main/resources/${mod_id}.accesswidener")
    if (aw.exists()) {
        accessWideners(aw)
    }
}

dependencies {
    compileOnly group:'org.spongepowered', name:'mixin', version:'0.8.5'
    if (useLocalYungsApi) {
        compileOnly files("libs/YungsApi-${mc_version}-Common-${yungsapi_version}.jar")
    } else {
        compileOnly("com.yungnickyoung.minecraft.yungsapi:YungsApi:${mc_version}-Common-${yungsapi_version}")
    }
}

configurations {
    commonJava {
        canBeResolved = false
        canBeConsumed = true
    }
    commonResources {
        canBeResolved = false
        canBeConsumed = true
    }
    commonGeneratedResources {
        canBeResolved = false
        canBeConsumed = true
    }
}

sourceSets {
    generated {
        resources {
            srcDir 'src/generated/resources'
        }
    }
}

artifacts {
    commonJava sourceSets.main.java.sourceDirectories.singleFile
    commonResources sourceSets.main.resources.sourceDirectories.singleFile
    commonGeneratedResources sourceSets.generated.resources.sourceDirectories.singleFile
}